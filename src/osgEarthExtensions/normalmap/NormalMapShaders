/* -*-c++-*- */
/* osgEarth - Dynamic map generation toolkit for OpenSceneGraph
* Copyright 2008-2012 Pelican Mapping
* http://osgearth.org
*
* osgEarth is free software; you can redistribute it and/or modify
* it under the terms of the GNU Lesser General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
*
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU Lesser General Public License for more details.
*
* You should have received a copy of the GNU Lesser General Public License
* along with this program.  If not, see <http://www.gnu.org/licenses/>
*/
#ifndef OSGEARTH_NORMALMAP_SHADERS
#define OSGEARTH_NORMALMAP_SHADERS 1

#undef  MULTILINE
#define MULTILINE(...) #__VA_ARGS__
namespace
{
//---------------------------------------------------------

    const char* NormalMapVertexShader = MULTILINE(

        \n#version 110\n

        attribute vec4 oe_terrain_attr;
        varying vec3 oe_Normal;

        varying vec3 oe_nmap_T;
        varying vec3 oe_nmap_B;
        varying vec3 oe_nmap_N;
        varying float oe_nmap_slope;

        void oe_nmap_vertex(inout vec4 VertexVIEW)
        {
            vec3 t = cross(vec3(0,0,1), oe_terrain_attr.xyz); // geocentric only
            vec3 b = cross(oe_terrain_attr.xyz, t);

            oe_nmap_T = normalize(t);
            oe_nmap_B = normalize(b);
            oe_nmap_N = oe_terrain_attr.xyz;
            
            oe_nmap_slope = clamp(2.5*(1.0-dot(gl_NormalMatrix * oe_terrain_attr.xyz, oe_Normal)),0.0,1.0);
        }
    );


//---------------------------------------------------------

    const char* NormalMapFragmentShader = MULTILINE(

        \n#version 110\n

        vec3 oe_global_Normal;

        uniform vec4 oe_tile_key;
        uniform sampler2D oe_nmap_tex;
        uniform float oe_nmap_intensity;
        uniform float oe_nmap_scale;
        
        varying vec3 oe_nmap_T;
        varying vec3 oe_nmap_B;
        varying vec3 oe_nmap_N;

        varying vec3 oe_nmap_up;
        varying float oe_nmap_slope;
        varying vec4 oe_layer_tilec;

        vec2 oe_nmap_scaleCoords(in vec2 coords, in float targetLOD)
        {            
            float dL = oe_tile_key.z - targetLOD;
            float factor = exp2(dL);
            float invFactor = 1.0/factor;
            vec2 scale = vec2(invFactor);
            vec2 result = coords * scale;

            // For upsampling we need to calculate an offset as well\n
            float upSampleToggle = factor >= 1.0 ? 1.0 : 0.0;
            {
                vec2 a = floor(oe_tile_key.xy * invFactor);
                vec2 b = a * factor;
                vec2 c = (a+1.0) * factor;
                vec2 offset = (oe_tile_key.xy-b)/(c-b);
                result += upSampleToggle * offset;
            }

            return result;
        }

        void oe_nmap_fragment(inout vec4 color)
        {
            // quantize the scale factor\n
            float iscale = float(int(oe_nmap_scale));
    
            // scale sampling coordinates to a target LOD.
            const float targetLOD = 13.0;
            vec2 coords = oe_nmap_scaleCoords(oe_layer_tilec.st, targetLOD);

            // sample the bump map, which is in tangent space.
            vec3 bump = normalize(texture2D(oe_nmap_tex, coords.st*iscale).xyz * 2.0 - 1.0);

            // The TBN "matrix" xforms tangent space vector to model space:
            vec3 T = normalize(oe_nmap_T);
            vec3 B = normalize(oe_nmap_B);
            vec3 N = normalize(oe_nmap_N);

            // Transform the bump vector: tangent->model->view.
            bump = normalize(gl_NormalMatrix * vec3(dot(bump,T), dot(bump,B), dot(bump,N)));

            // permute the normal with the bump.
            oe_global_Normal = normalize(oe_global_Normal + bump*oe_nmap_intensity*oe_nmap_slope);
        }

    );
}

#endif // OSGEARTH_NormalMap_NOISE_SHADERS
